plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
}

group 'net.thesimpleteam'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava {
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            pom {
                name = project.name
                description = project.description
                url = "https://github.com/Sunderia/SunderiaUtils"
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'minemobs'
                        name = 'minemobs'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'thesimpleteamRepo'
            def extension = version.toString().endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'
            url 'https://maven.thesimpleteam.net/' + extension
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}